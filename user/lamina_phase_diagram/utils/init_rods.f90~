PROGRAM INIT_RODS
IMPLICIT NONE
!This program iniitialises an array of N_RODS with N_SEGMENTS with a random angular perturbation
INTEGER :: N_ROD, N_SEG, N_SEED, CLOCK, I, J, K
INTEGER, ALLOCATABLE :: RAND_SEED(:), BONDS(:,:)
DOUBLE PRECISION :: L, H, RAND_MAG, DUMMY
DOUBLE PRECISION, ALLOCATABLE :: COORDS(:), INITPOS(:,:)

!------------------------------------------------------------------------------------------------------
!1) Read in the data file
!------------------------------------------------------------------------------------------------------
OPEN(1,FILE='data.in')
READ(1,*) N_ROD
READ(1,*) N_SEG
READ(1,*) L
READ(1,*) H
READ(1,*) DUMMY
READ(1,*) DUMMY
READ(1,*) DUMMY
READ(1,*) DUMMY
READ(1,*) DUMMY
READ(1,*) RAND_MAG
CLOSE(1)
!------------------------------------------------------------------------------------------------------


!------------------------------------------------------------------------------------------------------
!2) Allocate arrays
!------------------------------------------------------------------------------------------------------
ALLOCATE(COORDS(N_ROD*(N_SEG-1)), INITPOS(N_ROD,2), BONDS(3*N_SEG*(N_ROD-1),3))
!------------------------------------------------------------------------------------------------------

!------------------------------------------------------------------------------------------------------
!3) Make random coords
!------------------------------------------------------------------------------------------------------
CALL RANDOM_SEED(SIZE=N_SEED)
ALLOCATE(RAND_SEED(N_SEED))
CALL SYSTEM_CLOCK(COUNT=CLOCK)
RAND_SEED=CLOCK+26*(/(I-1,I=1,N_SEED)/)
CALL RANDOM_SEED(PUT=RAND_SEED)
CALL RANDOM_NUMBER(COORDS)
COORDS=2*RAND_MAG*(0.5-COORDS)
!------------------------------------------------------------------------------------------------------

!------------------------------------------------------------------------------------------------------
!4) Make initial coords
!------------------------------------------------------------------------------------------------------
DO I=1,N_ROD
	INITPOS(I,1)=0.0
	INITPOS(I,2)=(I-1.0)/(N_ROD-1.0)*H - H/2.0
ENDDO
!------------------------------------------------------------------------------------------------------


!------------------------------------------------------------------------------------------------------
!5) Make bonds
!------------------------------------------------------------------------------------------------------
K=1;
DO I = 1,N_ROD-1
    DO J=1,N_SEG+1
        
            ! -1 spring
            IF (J /= 1) THEN
                BONDS(K,1)=(I-1)*(N_SEG+1) + J
                BONDS(K,2)=(I)*(N_SEG+1) + J-1
                BONDS(K,3)=2
                K=K+1
            ENDIF
            
            ! 0 spring
            IF (J /= 1) THEN
                BONDS(K,1)=(I-1)*(N_SEG+1) + J
                BONDS(K,2)=(I)*(N_SEG+1) + J
                IF (J==N_SEG+1) THEN
                    BONDS(K,3)=3
                ELSE
                    BONDS(K,3)=1
                ENDIF
                K=K+1
            ENDIF
            
            ! +1 spring
            IF (J /= N_SEG+1) THEN
                BONDS(K,1)=(I-1)*(N_SEG+1) + J
                BONDS(K,2)=(I)*(N_SEG+1) + J+1
                BONDS(K,3)=2
                K=K+1
            ENDIF
 
	ENDDO
ENDDO
!------------------------------------------------------------------------------------------------------


!------------------------------------------------------------------------------------------------------
!6) Write outputs
!------------------------------------------------------------------------------------------------------
OPEN(1,FILE='coords')
WRITE(1,'(F20.10)') COORDS
CLOSE(1)

OPEN(1,FILE='initpos')
DO I=1,N_ROD
	WRITE(1,*) INITPOS(I,:)
ENDDO
CLOSE(1)

OPEN(1,FILE='bonds')
DO I=1,3*N_SEG*(N_ROD-1)
	WRITE(1,*) BONDS(I,:)
ENDDO
CLOSE(1)
!------------------------------------------------------------------------------------------------------

END PROGRAM INIT_RODS






































